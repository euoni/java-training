apply plugin: 'eclipse'
apply plugin: 'jacoco'

allprojects {
    apply plugin: 'java'
    repositories {
        mavenCentral()
    }
}

dependencies {
    compile 'com.github.jiconfont:jiconfont-swing:1.+'
    compile 'com.github.jiconfont:jiconfont-font_awesome:4.+'
    compile 'com.google.zxing:core:3.3.0'
    compile 'com.google.zxing:javase:3.3.0'
    compile (group: 'org.bytedeco', name: 'javacv-platform', version: '1.3.3') {
        exclude module: 'ffmpeg'
        exclude module: 'flycapture'
        exclude module: 'libdc1394'
        exclude module: 'libfreenect'
        exclude module: 'libfreenect2'
        exclude module: 'librealsense'
        exclude module: 'videoinput'
        exclude module: 'artoolkitplus'
    }
    testCompile 'junit:junit:4.+'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile project(':jpltest')
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

sourceSets.main.java.srcDirs = ['src']
sourceSets.main.resources.srcDirs = ['resources']
sourceSets.test.java.srcDirs = ['test']

def createGuiTask(def num) {
    return tasks.create("gui$num", Jar) {
        dependsOn jar
        baseName 'DigitalClock'
        manifest {
            attributes 'Main-Class' : "gui.ex${num}.DigitalClock"
        }
        from(sourceSets.main.output.classesDir) {
            include "gui/ex${num}/**"
        }
        from(sourceSets.main.output.resourcesDir) {
            include "*.ttf"
        }
        destinationDir file("src/gui/ex${num}/")
    }
}
createGuiTask('11')
createGuiTask('12')
createGuiTask('13')
createGuiTask('14')
createGuiTask('21')
createGuiTask('22')
createGuiTask('23')
createGuiTask('24')

task interpretJar(type: Jar) {
     dependsOn jar
     baseName 'interpret'
     manifest {
         attributes 'Main-Class' : 'interpret.MainWindow'
     }
     from(sourceSets.main.output.classesDir) {
         include 'interpret/**'
     }
     from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
     destinationDir file('src/interpret/')
}

task arclockJar(type: Jar) {
     dependsOn jar
     baseName 'arclock'
     manifest {
         attributes 'Main-Class' : 'arclock.Clock'
     }
     from(sourceSets.main.output.classesDir) {
         include 'arclock/**'
     }
     from(sourceSets.main.output.resourcesDir) {
         include "arclock/*.fxml"
     }
     exclude 'lib/armeabi/**'
     exclude 'fonts/**'
     exclude 'jiconfont/**'
     exclude 'org/bytedeco/javacpp/linux-armhf/**'
     exclude 'org/bytedeco/javacpp/linux-ppc64le/**'
     from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
     destinationDir file('src/arclock/')
}

task linkedListJavadoc(type: Javadoc) {
    source = sourceSets.main.allJava
    include("jpl/ch19/ex01/LinkedList.java")
}

task linkedListPrivateJavadoc(type: Javadoc) {
options.memberLevel = JavadocMemberLevel.PRIVATE
    source = sourceSets.main.allJava
    include("jpl/ch19/ex02/LinkedList.java")
}

project(':jpltest') {
    dependencies {
        compile 'junit:junit:4.+'
    }
    sourceSets.main.java.srcDirs = ['.']
    sourceSets.main.java.excludes = ['jpl/']
}

def defaultEncoding = 'UTF-8'
tasks.withType(AbstractCompile).each { it.options.encoding = defaultEncoding }
tasks.withType(GroovyCompile).each { it.groovyOptions.encoding = defaultEncoding }
