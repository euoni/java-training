apply plugin: 'eclipse'
apply plugin: 'jacoco'

allprojects {
    apply plugin: 'java'
    repositories {
        mavenCentral()
    }
}

dependencies {
    testCompile 'junit:junit:4.+'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile project(':jpltest')
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

sourceSets.main.java.srcDirs = ['src']
sourceSets.main.resources.srcDirs = ['resources']
sourceSets.test.java.srcDirs = ['test']

def createGuiTask(def num) {
    return tasks.create("gui$num", Jar) {
        dependsOn jar
        baseName name
        manifest {
            attributes 'Main-Class' : "gui.ex${num}.DigitalClock"
        }
        from(sourceSets.main.output.classesDir) {
            include "gui/ex${num}/**"
        }
        from sourceSets.main.output.resourcesDir
        destinationDir file('deploy')
    }
}
createGuiTask('11')
createGuiTask('12')
createGuiTask('13')

project(':jpltest') {
    dependencies {
        compile 'junit:junit:4.+'
    }
    sourceSets.main.java.srcDirs = ['.']
    sourceSets.main.java.excludes = ['jpl/']
}

def defaultEncoding = 'UTF-8'
tasks.withType(AbstractCompile).each { it.options.encoding = defaultEncoding }
tasks.withType(GroovyCompile).each { it.groovyOptions.encoding = defaultEncoding }
